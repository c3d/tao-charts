// ****************************************************************************
//  linechart.xl                                                    Tao project
// ****************************************************************************
//
//   File Description:
//
//     Define defaut linechart styles.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2012
// Contributors:
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jérôme Forissier <jerome@taodyne.com>
//   Catherine Burvelle <cathy@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao3D application.
//
// This software is licensed under the GNU General Public License v3
// See file COPYING for details.
// ****************************************************************************

// This file is designed to be overriden by themes, etc.
override_priority -1

// Default size of lines and points
theme_size Theme:text, "line", "line" -> 5
theme_size Theme:text, "line", "point" -> 15

// Default color of lines
theme_color Theme:text, "line", "line", N -> line_color_hsv N * 40, 1.0, 1.0
theme_color Theme:text, "line", Style:text, N -> color_hsv N * 40, 1.0, 1.0

// Default format of points
theme_format Theme:text, "line", "point", "3D" -> false

// ============================================================================
//
//                              Point Chart
//
// ============================================================================


theme_chart Theme:text, "line", "point", Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default point chart
// ----------------------------------------------------------------------------
    locally
        theme_format Theme, "line", "point", Format
        theme_color Theme, "line", "point", N

        // Compute coordinates of point
        if(chart_max_count > 1) then
            X := Index * (W / (chart_max_count - 1))
        else
            X := 0

        Y := Ratio * H + (chart_offset H)
        S := theme_size(Theme, "line", "point")
        point X, Y, 0, S


// ============================================================================
//
//                              Line Chart
//
// ============================================================================

// Previous coordinates
LINE_PREVIOUS_X -> 0.0
LINE_PREVIOUS_Y -> 0.0

theme_chart Theme:text, "line", "line", Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default line chart
// ----------------------------------------------------------------------------
    locally
        S := theme_size(Theme, "line", "line")

        // Set line width
        line_width S

        // Compute coordinates of line
        if (chart_max_count > 1) then
            X := Index * (W / (chart_max_count - 1))
        else
            X := 0

        Y := Ratio * H + (chart_offset H)

        // Do not show line for first index
        if (Index <> 0) then
            theme_format Theme, "line", "line", Format
            theme_color Theme, "line", "line", N
            path
                move_to LINE_PREVIOUS_X, LINE_PREVIOUS_Y
                endpoints_style NONE, NONE
                line_to X, Y

        // Save previous coordinates
        LINE_PREVIOUS_X := X
        LINE_PREVIOUS_Y := Y


// ============================================================================
//
//                       Complete Line Chart (points & lines)
//
// ============================================================================


theme_chart Theme:text, "line", Style:text, Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default point&line chart
// ----------------------------------------------------------------------------
    theme_chart Theme, "line", "point", Format, W, H, N, Set, Index, Ratio
    theme_chart Theme, "line", "line", Format, W, H, N, Set, Index, Ratio
