// ****************************************************************************
//  barchart.xl                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//     Define defaut bar chart styles.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2012
// Contributors:
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jérôme Forissier <jerome@taodyne.com>
//   Catherine Burvelle <cathy@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao3D application.
//
// This software is licensed under the GNU General Public License v3
// See file COPYING for details.
// ****************************************************************************

// This file is designed to be overriden by themes, etc.
override_priority -1

// Set bar colors
theme_color Theme:text, "bar", Style:text, N ->
    color_hsv N * 40, 1.0, 1.0


// ============================================================================
//
//    Default bar style
//
// ============================================================================


theme_bar Theme:text, Format:text, X:real, Y:real, W:real, H:real, N:integer ->
// ----------------------------------------------------------------------------
//    Default bar style of bar chart
// ----------------------------------------------------------------------------
    theme_format Theme, "bar", "bar", Format
    theme_color Theme, "bar", "bar", N
    rectangle X, Y, W, H


// ============================================================================
//
//    Vertical stacked bar chart
//
// ============================================================================

data bar_chart_positive_offsets
data bar_chart_negative_offsets

chart_get_bar_offset V:real -> V
chart_get_bar_offset Other -> 0

theme_chart Theme:text, "bar", "vertical_stacked", Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default stacked vertical bar chart
// ----------------------------------------------------------------------------
    I := Index // XL bug

    // Translate vertically data at the same index (not for first dataset)
    // NOTE: We seperate negative and positive offsets in order to draw
    // consecutively stacked bars
    offset -> 0.0
    offset := 0.0
    if(N = 0) then
        bar_chart_positive_offsets[I] := 0.0
        bar_chart_negative_offsets[I] := 0.0
    else
        if(Ratio > 0) then
            offset := (chart_get_bar_offset(bar_chart_positive_offsets[I]))
        else
            offset := (chart_get_bar_offset(bar_chart_negative_offsets[I]))

    // Get size of interval
    L := W / chart_max_count

    // Get size a single bar
    S := L / 1.3

    locally
        Y := offset

        // Translate data for the same dataset
        X := L * I

        // Add a translation to start intervals at X=0
        // (Avoid to display first bar at X=0)
        X := X + (S/2.0) + (L/2.0 - ((L/1.3)/2.0))

        // Readjust chart position to the origin (in case of negative values)
        Y := Y + (chart_offset H)

        theme_bar Theme, Format, X, Y + Ratio * (H / 2), S, Ratio * H, N

    // Update offset
    if(Ratio > 0) then
        bar_chart_positive_offsets[I] := Ratio * H + offset
    else
        bar_chart_negative_offsets[I] := Ratio * H + offset


theme_chart_xaxis Theme:text, "bar", "vertical_stacked", Format:text, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default x-axis for stacked vertical bar chart
// ----------------------------------------------------------------------------
    theme_chart_xaxis Theme, "bar", "vertical", Format, W, H


// ============================================================================
//
//    Horizontal stacked bar chart
//
// ============================================================================

theme_chart Theme:text, "bar", "horizontal_stacked", Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default stacked vertical bar chart
// ----------------------------------------------------------------------------
    I := Index // XL bug

    // Translate vertically data at the same index (not for first dataset)
    offset -> 0.0
    offset := 0.0
    if(N = 0) then
        bar_chart_positive_offsets[I] := 0.0
        bar_chart_negative_offsets[I] := 0.0
    else
        if(Ratio > 0) then
            offset := (chart_get_bar_offset(bar_chart_positive_offsets[I]))
        else
            offset := (chart_get_bar_offset(bar_chart_negative_offsets[I]))

    // Get size of interval
    L := H / chart_max_count

    // Get size a single bar
    S := L / 1.3

    locally
        X := offset

        // Translate data for the same dataset
        Y := L * I

        // Add a translation to start intervals at X=0
        // (Avoid to display first bar at X=0)
        Y := Y + (S/2.0) + (L/2.0 - ((L/1.3)/2.0))

        // Readjust chart position to the origin (in case of negative values)
        X := X + (chart_offset W)

        theme_bar Theme, Format, X + Ratio * (W / 2), Y, Ratio * W, S, N

    // Update offset
    if(Ratio > 0) then
        bar_chart_positive_offsets[I] := Ratio * W + offset
    else
        bar_chart_negative_offsets[I] := Ratio * W + offset


theme_chart_xaxis Theme:text, "bar", "horizontal_stacked", Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default x-axis for stacked horizontal bar chart
// ----------------------------------------------------------------------------
    theme_chart_xaxis Theme, "bar", "horizontal", Format, X, Y, W, H


theme_chart_yaxis Theme:text, "bar", "horizontal_stacked", Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default y-axis for stacked horizontal bar chart
// ----------------------------------------------------------------------------
    theme_chart_yaxis Theme, "bar", "horizontal", Format, X, Y, W, H


theme_chart_xlabel Theme:text, "bar", "horizontal_stacked", Body ->
// ----------------------------------------------------------------------------
//    Default x-label for stacked horizontal bar chart
// ----------------------------------------------------------------------------
    theme_chart_xlabel Theme, "bar", "horizontal", Body


theme_chart_ylabel Theme:text, "bar", "horizontal_stacked", Body ->
// ----------------------------------------------------------------------------
//    Default y-label for stacked horizontal bar chart
// ----------------------------------------------------------------------------
    theme_chart_ylabel Theme, "bar", "horizontal", Body


// ============================================================================
//
//    Horizontal bar chart
//
// ============================================================================

theme_chart Theme:text, "bar", "horizontal", Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default horizontal bar chart
// ----------------------------------------------------------------------------
    // Get size of interval
    L := H / chart_max_count

    // Get size a single bar
    S := L / (1.3 * chart_datasets_count)

    locally
        // Translate data for the same dataset
        Y := L * Index

        // Translate data at the same index for different datasets
        Y := Y + S * N

        // Readjust chart position to the origin (in case of negative values)
        X := chart_offset W

        // Add a translation to start intervals at X=0
        // (Avoid to display first bar at X=0)
        Y := Y + (S/2.0) + (L/2.0 - ((L/1.3)/2.0))

        theme_bar Theme, Format, X + Ratio * (W / 2), Y, Ratio * W, S, N


theme_chart_xaxis Theme:text, "bar", "horizontal", Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default x-axis for horizontal bar chart
// ----------------------------------------------------------------------------
    contents 0,
        translate X, Y, 0.0

        // Draw horizontal line
        theme_chart_axis Theme, "bar", W/2.0, 0, W, 0.5

        if(chart_yticks > 0) then
            // Compute y-interval length
            L := W / chart_yticks

            // Show y-label
            for I in 0..chart_yticks loop
                locally
                    translatex L * I

                    // Show vertical line
                    theme_chart_axis Theme, "bar", 0, H/2, 0.5, H

                    // Show y-label
                    text_box 0, -30, L, H,
                        theme_style Theme, "bar", "chart_tick"
                        text (chart_yticks_label I)


theme_chart_yaxis Theme:text, "bar", "horizontal", Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default y-axis for vertical bar chart
// ----------------------------------------------------------------------------
    contents 0,
        translate X, Y, 0.0

        // Draw horizontal line
        theme_chart_axis Theme, "bar", 0, H/2, 0.5, H

        if(chart_xticks > 0) then

            // Compute x-interval length
            L := H / chart_xticks

            // Show x-label below chart
            for I in 0..(chart_xticks - 1) loop
                locally
                    translatey L * I

                    // Show x label
                    text_box -40, (L/2.0), 60, L,
                        theme_style Theme, "bar", "chart_tick"
                        text (chart_xticks_label I)


theme_chart_xlabel Theme:text, "bar", "horizontal", Body ->
// ----------------------------------------------------------------------------
//    Default x-label for horizontal bar chart
// ----------------------------------------------------------------------------
    theme_chart_ylabel Theme, "bar", "", Body


theme_chart_ylabel Theme:text, "bar", "horizontal", Body ->
// ----------------------------------------------------------------------------
//    Default y-label for horizontal bar chart
// ----------------------------------------------------------------------------
    theme_chart_xlabel Theme, "bar", "", Body

// ============================================================================
//
//    Vertical bar chart
//
// ============================================================================

theme_chart Theme:text, "bar", Style:text, Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default vertical bar chart
// ----------------------------------------------------------------------------
    // Get size of interval
    L := W / chart_max_count

    // Get size a single bar
    S := L / (1.3 * chart_datasets_count)

    locally
        // Translate data for the same dataset
        X := L * Index

        // Translate data at the same index for different datasets
        X := X + S * N

        // Readjust chart position to the origin (in case of negative values)
        Y := chart_offset H

        // Add a translation to start intervals at X=0
        // (Avoid to display first bar at X=0)
        X := X + (S/2.0) + (L/2.0 - ((L/1.3)/2.0))

        theme_bar Theme, Format, X, Y + Ratio * (H / 2), S, Ratio * H, N


theme_chart_xaxis Theme:text, "bar", Style:text, Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default x-axis for vertical bar chart
// ----------------------------------------------------------------------------
    contents 0,
        translate X, Y, 0.0

        // Draw horizontal line
        theme_chart_axis Theme, "bar", W/2, 0, W, 0.5

        if(chart_xticks > 0) then

            // Compute x-interval length
            L := W / chart_xticks

            // Show x-label below chart
            for I in 0..(chart_xticks - 1) loop
                locally
                    translatex L * I

                    // Show x label
                    text_box (L/2.0), -30, L, 50,
                        theme_style Theme, "bar", "chart_tick"
                        text (chart_xticks_label I)
