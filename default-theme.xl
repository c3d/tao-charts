// *****************************************************************************
// default-theme.xl                                                Tao3D project
// *****************************************************************************
//
// File description:
//
//     Define a default theme for charts.
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2013-2014, Baptiste Soulisse <baptiste.soulisse@taodyne.com>
// (C) 2013, Catherine Burvelle <catherine@taodyne.com>
// (C) 2013-2014,2019, Christophe de Dinechin <christophe@dinechin.org>
// (C) 2013-2014, Jérôme Forissier <jerome@taodyne.com>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************


import Slides

// This file is designed to be overriden by themes, etc.
override_priority -2

// Compute offset needed to adjust position
// of charts (case of negative values)
chart_offset R:real -> -(chart_min_axis * R) / (chart_max_axis - chart_min_axis)

import "area_chart.xl"
import "bar_chart.xl"
import "line_chart.xl"
import "pie_chart.xl"

// Default theme size of charts
chart_width  -> theme_drawing_width theme
chart_height -> theme_drawing_height theme


// ============================================================================
//
//    Easily configurable parameters
//
// ============================================================================

// Default size of charts components
theme_size Theme:text, Master:text, "chart_data" -> 20
theme_size Theme:text, Master:text, "chart_label" -> 25
theme_size Theme:text, Master:text, "chart_tick" -> 20
theme_size Theme:text, Master:text, "chart_title" -> 40
theme_size Theme:text, Master:text, "chart_legend" -> 30

// Default color of charts components
theme_color Theme:text, Master:text, "chart_legend", N:integer -> theme_color Theme, Master,"story"

// Default format of charts components
theme_format Theme:text, Master:text, "chart_data", "3D" -> translatez 50
theme_format Theme:text, Master:text, Style:text, "3D" ->
    extrude_depth 50
    translatez 50

theme_format Theme:text, Master:text, Style:text, Format:text -> false

// Default alignment of charts components
theme_align Theme:text, Master:text, "chart_data" -> align 0.5; vertical_align 0.5
theme_align Theme:text, Master:text, "chart_tick" -> align 0.5; vertical_align 0.5
theme_align Theme:text, Master:text, "chart_label" -> align 0.5; vertical_align 0.45
theme_align Theme:text, Master:text, "chart_title" -> align 0.5; vertical_align 0.5
theme_align Theme:text, Master:text, "chart_entry", N:integer -> theme_align Theme,Master,"chart_legend",N
theme_align Theme:text, Master:text, "chart_legend" ->
    align 0.0
    vertical_align 0.5
    paragraph_space 0, 30
    margins 90, 0


// ============================================================================
//
//    Charts Layout
//
// ============================================================================

theme_chart_layout Theme:text, Master:text, Style:text, Format:text ->
// ----------------------------------------------------------------------------
//    Default chart layout
// ----------------------------------------------------------------------------
    // Draw main box
    box "chart", 0, 0, chart_width, chart_height

    // Draw axis
    chart_xaxis -800, -275, 1200, 500, Style, Format
    chart_yaxis -800, -275, 1200, 500, Style, Format

    chart_flow 0, 325, 800, 50, "chart_title"
    chart_flow 435, -275, 100, 50, "chart_xlabel"
    chart_flow -800, 250, 100, 50, "chart_ylabel"
    chart_flow 725, -30, 300, 600, "chart_legend"

    // Draw chart
    box Master, -175, -30, 1400, 600
    chart_series -800, -275, 1200, 500, Master, Style, Format


// ============================================================================
//
//    Chart
//
// ============================================================================

theme_chart Theme:text, Master:text, Style:text, Format:text, W:real, H:real, N:integer, Set:integer, Index:integer ->
// ----------------------------------------------------------------------------
//    Display a unique data in a chart
// ----------------------------------------------------------------------------
    // Compute ratio for current data
    Ratio := chart_data_ratio Set, Index
    theme_chart Theme, Master, Style, Format, W, H, N, Set, Index, Ratio


// Default chart style
theme_chart Theme:text, Master:text, Style:text, Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real -> false

// ============================================================================
//
//    Data
//
// ============================================================================

theme_text Theme:text,Master:text,"chart_data",X:real,Y:real,W:real,H:real,Body ->
// ----------------------------------------------------------------------------
//    Override text implementation (don't show any box)
// ----------------------------------------------------------------------------
    theme_text_without_box Theme, Master, "chart_data", X, Y, W, H, Body


// ============================================================================
//
//    Title
//
// ============================================================================

theme_chart_title Theme:text, Master:text, Style:text, Body ->
// ----------------------------------------------------------------------------
//    Default implementation of chart title
// ----------------------------------------------------------------------------
    chart_text_flow "chart_title",
        theme_style Theme, Master, "chart_title"
        Body


// ============================================================================
//
//    Legend
//
// ============================================================================

theme_chart_legend Theme:text, Master:text, Style:text, Body ->
// ----------------------------------------------------------------------------
//    Default implementation of chart title
// ----------------------------------------------------------------------------
    chart_text_flow "chart_legend",
        theme_style Theme, Master, "chart_legend"
        Body


theme_chart_entry Theme:text, Master:text, Style:text, N:integer, Body ->
// ----------------------------------------------------------------------------
//   Default style of legend entry
// ----------------------------------------------------------------------------
    text_span
        theme_style Theme, Master, "chart_legend"
        paragraph_break
        anchor
            locally
                theme_color Theme, Master, "chart_entry", N
                S := theme_size Theme, Master, "chart_legend"
                rectangle -2 * S, S/3.0, 2 * S, S
        Body


// ============================================================================
//
//    Axis label
//
// ============================================================================

theme_chart_xlabel Theme:text, Master:text, Style:text, Body ->
// ----------------------------------------------------------------------------
//    Default implementation of x-label
// ----------------------------------------------------------------------------
    chart_text_flow "chart_xlabel",
        theme_style Theme, Master, "chart_label"
        Body


theme_text Theme:text,Master:text,"chart_xlabel",X:real,Y:real,W:real,H:real,Body ->
// ----------------------------------------------------------------------------
//    Override text implementation (don't show any box)
// ----------------------------------------------------------------------------
    theme_text_without_box Theme, Master, "chart_xlabel", X, Y, W, H, Body


theme_chart_ylabel Theme:text, Master:text, Style:text, Body ->
// ----------------------------------------------------------------------------
//    Default implementation y-label
// ----------------------------------------------------------------------------
    chart_text_flow "chart_ylabel",
        theme_style Theme, Master, "chart_label"
        Body


theme_text Theme:text,Master:text,"chart_ylabel",X:real,Y:real,W:real,H:real,Body ->
// ----------------------------------------------------------------------------
//    Override text implementation (don't show any box)
// ----------------------------------------------------------------------------
    theme_text_without_box Theme, Master, "chart_ylabel", X, Y, W, H, Body


// ============================================================================
//
//    Axis
//
// ============================================================================

theme_chart_axis Theme:text, Master:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default style of an axis
// ----------------------------------------------------------------------------
    locally
        translatez -1 // Avoid depth problems
        theme_color Theme, Master, "axis"
        rectangle X, Y, W, H


theme_chart_xaxis Theme:text, Master:text, Style:text, Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default style of x-axis
// ----------------------------------------------------------------------------
    contents 0,
        translate X, Y, 0.0

        theme_chart_axis Theme, Master, W/2, 0, W, 0.5

        // Compute x-interval length
        if(chart_xticks > 1) then
            L := W / (chart_xticks - 1)
        else
            L := 0

        // Show x-label below chart
        for I in 0..(chart_xticks - 1) loop
            locally
                translatex L * I

                // Show vertical line
                theme_chart_axis Theme, Master, 0, H/2, 0.5, H

                // Get x-label
                Label := do chart_xticks_label I

                text_box 0, -30, L, 50,
                    theme_style Theme, Master, "chart_tick"
                    text Label


theme_chart_yaxis Theme:text, Master:text, Style:text, Format:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Default style of y-axis
// ----------------------------------------------------------------------------
    contents 0,
        translate X, Y, 0.0

        theme_chart_axis Theme, Master, 0, H/2, 0.5, H

        if(chart_yticks > 0) then
            // Compute y-interval length
            L := H / chart_yticks

            // Show y-label
            for I in 0..chart_yticks loop
                locally
                    translatey L * I

                    // Show horizontal line
                    theme_chart_axis Theme, Master, W/2, 0, W, 0.5

                    // Get y-label
                    Label := chart_yticks_label I
                    text_box -40, 0, 50, L,
                        theme_style Theme, Master, "chart_tick"
                        text Label


// ============================================================================
//
//   Texts without boxes
//
// ============================================================================

theme_text_without_box Theme:text,Master:text,Style:text,X:real,Y:real,W:real,H:real,Body ->
// ----------------------------------------------------------------------------
//   Draw a default text box
// ----------------------------------------------------------------------------
    contents 0,
        text_box X, Y, W, H,
            theme_style Theme, Master, Style
            Body
