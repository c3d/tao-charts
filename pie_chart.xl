// ****************************************************************************
//  piechart.xl                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//     Define defaut piechart styles.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2012
// Contributors:
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jérôme Forissier <jerome@taodyne.com>
//   Catherine Burvelle <cathy@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************

// This file is designed to be overriden by themes, etc.
override_priority -1

// Set pie colors
theme_color Theme:text, "pie", Style:text, N:integer ->
    color_hsv (N - 1) * 360.0 / (chart_count chart_first), 1.0, 1.0

theme_boxstyle Theme:text, Master:text, "pie" -> color "white",0.0

theme_format Theme:text, "pie", "chart_data", "3D" ->  translate 0, 30, 200; rotatex -35
theme_format Theme:text, "pie", Style:text, "3D" ->
    extrude_depth 50
    translate 0, 30, 200
    rotatex -35

theme_align Theme:text, "pie", "chart_legend" ->
    align 0.0
    vertical_align 0.5
    paragraph_space 0, (30.0 / (chart_count chart_first))
    margins 90, 0

// ============================================================================
//
//    Pie chart layout
//
// ============================================================================

theme_chart_layout Theme:text, "pie", Style:text, Format:text ->
// ----------------------------------------------------------------------------
//    Default pie chart layout
// ----------------------------------------------------------------------------
    // Draw main box
    box "chart", 0, 0, chart_width, chart_height

    // Draw components
    chart_flow 0, 325, 800, 50, "chart_title"
    chart_flow 725, -30, 300, 600, "chart_legend"

    // Draw chart
    box "pie", 0, -30, 600, 600
    chart_series 0, -25, 600, 600, "pie", Style, Format

// ============================================================================
//
//    Default pie chart
//
// ============================================================================


// Offset angle to start current pie
// at the end of the previous
pie_chart_offset -> 0.0


theme_chart Theme:text, "pie", Style:text, Format:text, W:real, H:real, N:integer, Set:integer, Index:integer, Ratio:real ->
// ----------------------------------------------------------------------------
//    Default pie chart
// ----------------------------------------------------------------------------
    if(Index = 0) then
        pie_chart_offset := 0.0

    // Compute angle of pie
    Angle := abs(Ratio) * 360

    // Compute the semi-angle of current pie
    Angle2 := ((Angle / 2.0 + 90 + pie_chart_offset) * pi) / 180

    // Compute offset between each pie
    Offset := (chart_data_property(Set, Index, "exploded_slice"))

    locally
        // Show data value at the middle of the pie
        translate Offset/2.0 * cos Angle2, Offset/2.0 * sin Angle2, 1

        locally
            theme_format Theme, "pie", "pie", Format
            theme_color Theme, "pie", "pie", (Index + 1)
            rotatez 90
            ellipse_sector 0, 0, H, W, pie_chart_offset, Angle

        locally
            theme_format Theme, "pie", "chart_data", Format

            V := text abs(chart_data(Set, Index))
            X := W * 0.35 * cos Angle2
            Y := H * 0.35 * sin Angle2
            VW := text_width V
            VH := text_height V

            // Show value
            theme_text Theme, "pie", "chart_data", X, Y, VW, VH,
                text V


    // Update offset
    pie_chart_offset := pie_chart_offset + Angle


// ============================================================================
//
//    Others
//
// ============================================================================

// Default axis for others charts
theme_chart_xaxis Theme:text, "pie", Style:text, Format:text, W:real, H:real -> false
theme_chart_yaxis Theme:text, "pie", Style:text, Format:text, W:real, H:real -> false
